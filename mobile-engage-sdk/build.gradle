apply from: 'dev.gradle'
apply from: 'version.gradle'
apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.getkeepsafe.dexcount'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 26
        versionCode gitVersionCode
        versionName gitVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "ME_BASE_V2_URL", System.env.ME_BASEV2_URL ?: project.ME_BASE_V2_URL
        buildConfigField "String", "ME_BASE_V3_URL", System.env.ME_BASE_V3_URL ?: project.ME_BASE_V3_URL
        buildConfigField "String", "INBOX_BASE_URL", System.env.INBOX_BASE_URL ?: project.INBOX_BASE_URL
        buildConfigField "String", "DEEP_LINK_BASE_URL", System.env.DEEP_LINK_BASE_URL ?: project.DEEP_LINK_BASE_URL
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

group = 'com.emarsys'
version = gitVersionName

dependencies {
    if (project.ext.devConfig.useLatestArtifacts || System.env.DEVELOPMENT_MODE) {
        compile 'com.emarsys:core-sdk:+'
    } else {
        compile 'com.emarsys:core-sdk:1.4.0'
    }

    compile 'com.android.support:appcompat-v7:26.0.2'
    compile "com.android.support:support-annotations:26.0.2"
    compile 'com.google.firebase:firebase-core:10.2.1'
    compile 'com.google.firebase:firebase-messaging:10.2.1'
    compile('com.android.support.test.espresso:espresso-idling-resource:2.2.2')

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.12.0'
    androidTestCompile 'org.mockito:mockito-android:2.12.0'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

apply from: 'maven.gradle'
apply from: 'bintray.gradle'
